// db <<<
// 0, 42
// 1, 43
// 3, 1 1 1 1
// >>>

storage {
    x: int,
    y: int,
    z: int,
    w: b256,
}

predicate First(x: int) { 
    constraint x == 1;
}

predicate Bar(x: int) { 
     constraint x == 2;
}

predicate Foo(
    tuple: { int, int, { b256, bool }},
) {
    // These addresses can be verified by compiling this contract using the `pint` CLI tool which
    // produces the address of every predicate.
    constraint __address_of("::First") ==
        0x903B6E8EC1A5B69E5951BB80900849A5C0F7198D2862E10EFC5A636FBAB40DA5;
    constraint __address_of("::Bar") ==
        0xEC17ED988BA809A88C204BA012AF0FC22C2D539E42E94BE94E573CFBCEA74032;

    constraint __sha256(0) == 0xAF5570F5A1810B7AF78CAF4BC70A660F0DF51E42BAF91D4DE5B2328DE0E83DFC;

    constraint tuple == { 1, 2, { 0x0000000000000001000000000000000200000000000000030000000000000004, true } };

    constraint __sha256(tuple) == 0x18D301B2F25338A9008E8EADB74406D01C6BE8F0C51BF782AD926E9F82D28EC3;

    // sign the data usign private key corresponding to the public key below. Plug in the data, the
    // signature, and the public key and make sure that `verify_ed25519` is `true`.
    // These values are tested in Rust in the `essential-base` repo
    constraint __verify_ed25519 (
            // data
            [7, 3, 5, 7], 
            // Signature
            {
                0xf83fdf913db5d1a1797c995d0c6fab555216ff8d5ef0c4d4084b32a0017682f3,
                0xb8e68aaeef9c37c02740749d91a791af69f50d75471208bbcb52e1a0e7926f09
            },
            // public key
            0x6d9b7578933cb6bddeb714f69f44af4b38b44d813ce574841f5afef78888043e
        );

    // Provide a zero hash and a signature. Must recover back a public key
    // These values are tested in Rust in the `essential-base` repo
    constraint __recover_secp256k1 (
            0x0000000000000000000000000000000000000000000000000000000000000000,
            // Signature
            {
                0xa3aa87b6fb725c774f7136b8ab244ff64e5c4c3623fe14c130432754bb985d56,
                0x3db2fa0dc433275d67756a15cadeb73bf703dd8458c2c0874684f6334dc177f5,
                1,                
            },
        ) == {
            0x0308f06a2a35c484909a2480ce681b24967cfd55fca22fcd705769ac55db6279,
            13
        };

    let x = storage::x;
    let y = storage::y;
    let z = storage::z;
    let w = storage::w;

    // `x` is set in the pre let db and in the solution
    // `x` is an optional
    constraint __size_of(x) == 1 + 1;
    constraint __size_of(x') == 1 + 1;

    // `y` is set in the pre let db and in the solution
    // `y` is an `int` 
    constraint __size_of(y!) == 1;
    constraint __size_of(y'!) == 1;

    // `z` is not set in the pre let db but is set in the solution
    // `z` is an optional
    constraint __size_of(z) == 1 + 1;
    constraint __size_of(z') == 1 + 1;

    // `w` is set in the pre let db but unset in the solution
    // `w` is an optional
    constraint __size_of(w) == 1 + 4;
    constraint __size_of(w') == 1 + 4;

    // `__size_of` called on non-let vars.
    constraint __size_of(1) == 1;
    constraint __size_of(tuple) == 7;
    constraint __size_of(__size_of(w')) == 1;
}
